plugins {
    id 'java'
    id 'application'
}

String buildNumber = 'git rev-list --count HEAD'.execute().text.trim();

version "0.0.6.${buildNumber}-SNAPSHOT"

mainClassName = 'com.github.mouse0w0.peach.Peach'

dependencies {
    compile project('peach-controls')
    compile project('mc-hierarchy')

    compile 'com.google.guava:guava:31.0.1-jre'
    compile 'org.apache.commons:commons-lang3:3.12.0'
    compile 'org.apache.commons:commons-collections4:4.4'
    compile 'commons-io:commons-io:2.11.0'
    compile 'com.google.code.gson:gson:2.8.9'
    compile 'org.dom4j:dom4j:2.1.3'
    compile 'com.github.sps.junidecode:junidecode:0.3'

    compile 'com.github.mouse0w0:eventbus:2.6.0'
    compile 'com.github.mouse0w0:version:1.4.0'
    compile 'com.github.mouse0w0:i18n:1.3.1'
    compile 'com.github.mouse0w0:gridview:1.0.0'
    compile 'com.github.mouse0w0:viewpane:8.0.0'
    compile 'com.github.mouse0w0:coffeemaker:3.0.0'

    compile 'org.controlsfx:controlsfx:8.40.18'

    compile 'org.ow2.asm:asm-commons:9.0'
    compile 'org.codehaus.groovy:groovy:3.0.9'

    compile 'org.slf4j:slf4j-api:1.7.32'

    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.15.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.32'
}

jar {
    String gitBranch = 'git name-rev --name-only HEAD'.execute().text.trim()
    String gitVersion = 'git rev-parse --short HEAD'.execute().text.trim()
    manifest.attributes([
            "Main-Class"              : mainClassName,
            "Implementation-Title"    : rootProject.name,
            "Implementation-Version"  : project.version,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "Git-Branch"              : gitBranch,
            "Git-Version"             : gitVersion,
            "Git-BuildNumber"         : buildNumber
    ])
}

distributions {
    main {
        contents {
            from('content-pack') { into 'content-pack' }
            from('template') { into 'template' }
            from('license') { into 'license' }
            from('peach.bat', 'CHANGELOG.md')
        }
    }
}

allprojects {
    group 'com.github.mouse0w0'

    sourceCompatibility = targetCompatibility = 1.8

    repositories {
        maven { url 'https://maven.aliyun.com/repository/public' }
        mavenCentral()
        maven { url 'https://jitpack.io' }
        mavenLocal()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}
